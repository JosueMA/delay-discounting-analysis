# JAGS model for delay discounting.
# - model of multiple participants
# - models the magnitude effect
# - separate parameters (for each participant) for:
#   - m
#   - c
#   - epsilon (lapse rate)
#   - alpha
# - no Hierarchical estimation at all

model{

  # Separate and independent priors for each participant.
  for (p in 1:nParticipants){
      m[p]       ~ dnorm(-0.243, 1/(10^2))
      c[p]       ~ dnorm(0, 1/(100^2))
      alpha[p]   ~ dexp(0.01)
      epsilon[p] ~ dbeta(1.1, 10.9) T(,0.5)
  }

  # These priors are separated from the data and are only included here
	# so that we can plot the prior distributions. They are duplicated from
	# the priors above.
  m_prior       ~  dnorm(-0.243, 1/(10^2))
  c_prior       ~  dnorm(0, 1/(100^2))
  alpha_prior   ~  dexp(0.01)
  epsilon_prior ~  dbeta(1.1, 10.9) T(,0.5)

	# *********************************************************************

  for (p in 1:nParticipants){

      # loop over the number of trials for this participant
      for (t in 1:T[p]) {

          # MAGNITUDE EFFECT
          # what is log(k) on this trial?
          lkA[p,t] <- m[p]*log(abs(A[p,t]))+c[p]
          lkB[p,t] <- m[p]*log(abs(B[p,t]))+c[p]

          # calculate present subjective value for each reward
          VA[p,t] <- A[p,t] / (1+(exp(lkA[p,t])*DA[p,t]))
          VB[p,t] <- B[p,t] / (1+(exp(lkB[p,t])*DB[p,t]))

          # Psychometric function
          P[p,t] <- epsilon[p] + (1-2*epsilon[p]) * phi( (VB[p,t]-VA[p,t]) / alpha[p] )

          # response
          R[p,t] ~ dbern(P[p,t])

          # posterior predictive
          Rpostpred[p,t] ~ dbern(P[p,t])
      }
  }
}
