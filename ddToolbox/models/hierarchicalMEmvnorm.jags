model{

#    # priors over group M mean
#    groupMmu_MEAN <- -0.243
#    groupMmu_PRECISION <- 1/( (0.027*10)^2)
#    # priors over group M sigma
#    groupMsigma_MEAN <- 0.072
#    groupMsigma_PRECISION <- 1/( (0.025*10)^2)

    # ----------------------------------------------------------------
    # Group-level hyperpriors (see Appendix A1 and A2 for details)

#    # slope (uninformative, see Appendix A)
#    groupMmu        ~ dnorm(groupMmu_MEAN, groupMmu_PRECISION)
#    groupMsigma     ~ dnorm(groupMsigma_MEAN, groupMsigma_PRECISION) T(0,)
#
#    # intercept
#    groupCmu        ~ dnorm(0, 1/(10000^2))
#    groupCsigma     ~ dunif(0, 10000)

    # comparison acuity (alpha)
    groupALPHAmu        ~ dunif(0,100)
    groupALPHAsigma     ~ dunif(0,100)

    # error rates (epsilon)
    groupW          ~ dbeta(1.1, 10.9)  # mode for lapse rate
    groupKminus2    ~ dgamma(0.01,0.01) # concentration parameter
    groupK          <- groupKminus2+2



    mc_mu[1] ~ dnorm(-0.243, 1/( (0.027*10)^2))    # prior over m_mu
    mc_mu[2] ~ dnorm( 0    , 1/( 10^2) )           # prior over c_mu

    lambda[1] ~ dgamma(0.001,0.001)
    lambda[2] ~ dgamma(0.001,0.001)
    r ~ dunif(-1,1)

    # Reparamaterize
    mc_sigma[1] <- 1/sqrt(lambda[1]) # sigma over m
    mc_sigma[2] <- 1/sqrt(lambda[2]) # sigma over c
    T[1,1] <- 1/lambda[1]
    T[1,2] <- r*mc_sigma[1]*mc_sigma[2]
    T[2,1] <- r*mc_sigma[1]*mc_sigma[2]
    T[2,2] <- 1/lambda[2]
    TI[1:2,1:2] <- inverse(T[1:2,1:2])

    # ---------------------------------------------------------------
    # Participant-level parameters
    for (p in participantIndexList){

      mc[p,1:2]   ~ dmnorm(mc_mu[],TI[,])

      # just for ease of plotting variables m, c...
      m[p] <- mc[p,1]
      c[p] <- mc[p,2]

      epsilon[p]  ~ dbeta(groupW*(groupK-2)+1 , (1-groupW)*(groupK-2)+1 ) T(,0.5)
      alpha[p]    ~ dnorm(groupALPHAmu, 1/(groupALPHAsigma^2)) T(0,)
    }


  for (t in 1:length(ID)) {
        # Calculate log discount rate for each reward
        lkA[t] <- mc[ID[t],1] * log(abs(A[t])) + mc[ID[t],2]
        lkB[t] <- mc[ID[t],1] * log(abs(B[t])) + mc[ID[t],2]

        # calculate present subjective value for each reward
        VA[t] <- A[t] / (1+ ( exp(lkA[t]) * DA[t]))
        VB[t] <- B[t] / (1+ ( exp(lkB[t]) * DB[t]))

        # Psychometric function
        P[t] <- epsilon[ID[t]] + (1-2*epsilon[ID[t]]) * phi( (VB[t]-VA[t]) / alpha[ID[t]] )

        # response likelihood
        R[t]         ~ dbern(P[t]) # likelihood of actual response
        Rpostpred[t] ~ dbern(P[t]) # posterior predicted response
    }



    # #############################################
    # GENERATED QUANTITIES ########################

    # Sample hyperpriors (Make sure values correspond to the priors given above)
    # We draw these samples, that are independent from the data, so that we can plot our prior beliefs of group-level parameters. These will be determined by the actual values given in the priors.
    # IMPORTANT - Make sure the numerical values you provide below match the priors above.


    # comparison acuity
    groupALPHAmu_prior    ~ dunif(0,1000)
    groupALPHAsigma_prior ~ dunif(0,1000)

    # error rates
    groupW_prior          ~ dbeta(1.1, 10.9)  # mode for lapse rate
    groupKminus2_prior    ~ dgamma(0.01,0.01) # concentration parameter
    groupK_prior          <- groupKminus2_prior+2



    # Group-level posterior predictive distributions. These samples can be seen as inferences about an as yet unobserved participant who represents what we know about the parameters at the group level.

    lambda_group_prior[1] ~ dgamma(0.001,0.001)
    lambda_group_prior[2] ~ dgamma(0.001,0.001)
    mc_sigma_group_prior[1] <- 1/sqrt(lambda_group_prior[1]) # sigma over m
    mc_sigma_group_prior[2] <- 1/sqrt(lambda_group_prior[2]) # sigma over c
    T_group_prior[1,1] <- 1/lambda[1]
    T_group_prior[1,2] <- r*mc_sigma[1]*mc_sigma[2]
    T_group_prior[2,1] <- r*mc_sigma[1]*mc_sigma[2]
    T_group_prior[2,2] <- 1/lambda[2]
    TI_group_prior[1:2,1:2] <- inverse(T_group_prior[1:2,1:2])
    mc_mu_group_prior[1] ~ dnorm(-0.243, 1/( (0.027*10)^2))    # prior over m_mu
    mc_mu_group_prior[2] ~ dnorm( 0    , 1/( 10^2) )           # prior over c_mu
    mc_group[1:2]   ~  dmnorm(mc_mu_group_prior[],TI_group_prior[,])
    m_group         <- mc_group[1]
    c_group         <- mc_group[2]



    alpha_group         ~ dnorm(groupALPHAmu, 1/(groupALPHAsigma^2)) T(0,)
    epsilon_group       ~ dbeta(groupW*(groupK-2)+1 , (1-groupW)*(groupK-2)+1 ) T(,0.5)

    # Prior beliefs about the group level (equivalently, an as yet unobserved participant). As determined by priors on parameters specified above
    alpha_group_prior    ~ dnorm(groupALPHAmu_prior, 1/(groupALPHAsigma_prior^2)) T(0,)
    epsilon_group_prior  ~ dbeta(groupW_prior*(groupK_prior-2)+1 , (1-groupW_prior)*(groupK_prior-2)+1 ) T(,0.5)

}
