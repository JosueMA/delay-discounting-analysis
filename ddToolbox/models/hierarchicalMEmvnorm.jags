model{

# comparison acuity (alpha)
groupALPHAmu        ~ dunif(0,100)
groupALPHAsigma     ~ dunif(0,100)

# error rates (epsilon)
groupW          ~ dbeta(1.1, 10.9)  # mode for lapse rate
groupKminus2    ~ dgamma(0.01,0.01) # concentration parameter
groupK          <- groupKminus2+2


# Multivariate normal distribution over (m,c) =========================
mc_mu[1] ~ dnorm(-0.243, 1/( (0.027*10)^2))    # prior over m_mu
mc_mu[2] ~ dnorm(-3    , 1/( 10^2) )           # prior over c_mu

lambda[1] ~ dgamma(0.001,0.001)
lambda[2] ~ dgamma(0.001,0.001)

#r ~ dunif(-1,1)
r ~ dnorm(-0.9,1/(0.2^2)) T(-1,1)

# Reparamaterize
mc_sigma[1] <- 1/sqrt(lambda[1]) # sigma over m
mc_sigma[2] <- 1/sqrt(lambda[2]) # sigma over c
T[1,1] <- 1/lambda[1]
T[1,2] <- r*mc_sigma[1]*mc_sigma[2]
T[2,1] <- r*mc_sigma[1]*mc_sigma[2]
T[2,2] <- 1/lambda[2]
TI[1:2,1:2] <- inverse(T[1:2,1:2])
# =====================================================================



for (p in participantIndexList){
	mc[p,1:2]   ~ dmnorm(mc_mu[],TI[,])

	# just for ease of plotting variables m, c...
	m[p] <- mc[p,1]
	c[p] <- mc[p,2]

	epsilon[p]  ~ dbeta(groupW*(groupK-2)+1 , (1-groupW)*(groupK-2)+1 ) T(,0.5)
	alpha[p]    ~ dnorm(groupALPHAmu, 1/(groupALPHAsigma^2)) T(0,)
}

for (t in 1:length(ID)) {
	# Calculate log discount rate for each reward
	lkA[t] <- mc[ID[t],1] * log(abs(A[t])) + mc[ID[t],2]
	lkB[t] <- mc[ID[t],1] * log(abs(B[t])) + mc[ID[t],2]

	# calculate present subjective value for each reward
	VA[t] <- A[t] / (1+ ( exp(lkA[t]) * DA[t]))
	VB[t] <- B[t] / (1+ ( exp(lkB[t]) * DB[t]))

	# Psychometric function
	P[t] <- epsilon[ID[t]] + (1-2*epsilon[ID[t]]) * phi( (VB[t]-VA[t]) / alpha[ID[t]] )

	# response likelihood
	R[t]         ~ dbern(P[t]) # likelihood of actual response
	Rpostpred[t] ~ dbern(P[t]) # posterior predicted response
}

}
