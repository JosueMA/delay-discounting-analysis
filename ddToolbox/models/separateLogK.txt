model{

  logk_MEAN <- log(1/365)
  logk_PRECISION <- 1/(10^2)

  # Participant-level parameters
  for (p in 1:nParticipants){
    # magnitude effect (m,c) for each person
    logk[p]     ~ dnorm(logk_MEAN, logk_PRECISION)
    epsilon[p]  ~ dbeta(1.1 , 10.9) T(,0.5)
    alpha[p]    ~ dexp(0.01)
  }

  for (p in 1:nParticipants){
    for (t in 1:T[p]) {
      # calculate present subjective value for each reward
      VA[p,t] <- A[p,t] / (1+(exp(logk[p])*DA[p,t]))
      VB[p,t] <- B[p,t] / (1+(exp(logk[p])*DB[p,t]))

      # Psychometric function
      P[p,t] <- epsilon[p] + (1-2*epsilon[p]) * phi( (VB[p,t]-VA[p,t]) / alpha[p] )

      # response
      R[p,t] ~ dbern(P[p,t])
    }
  }


  # #############################################
  # GENERATED QUANTITIES ########################

  # priors (scalar)
  logk_prior     ~ dnorm(logk_MEAN, logk_PRECISION)
  epsilon_prior  ~ dbeta(1.1 , 10.9) T(,0.5)
  alpha_prior    ~ dexp(0.01)

  for (p in 1:nParticipants){
    for (t in 1:T[p]) {
      Rpostpred[p,t] ~ dbern(P[p,t])
    }
  }
}
