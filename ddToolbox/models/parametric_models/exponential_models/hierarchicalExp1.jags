# RANDOM FACTORS:   k[p], epsilon[p], alpha[p]
# HYPER-PRIORS ON:  k[p], epsilon[p], alpha[p]

# RANDOM (by participant) FACTORS APPEAR IN A LOOP OVER PARTICIPANTS
# VARIABLES *WITH* GROUP SHRINKAGE HYPERPRIORS HAVE AN EXTRA (UNOBSERVED) PARTICIPANT

model{


# DISCOUNT FUNCTION PARAMETERS =================================================
# RANDOM (BY PARTICIPANT) FACTORS; HYPER-PRIORS = YES 

groupKmu      ~ dnorm(0.01, 1/(0.5^2))
groupKsigma   ~ dgamma(0.1, 0.1)

for (p in 1:(nRealExperimentFiles+1)){      # +1 for unobserved participant
	k[p]        ~ dnorm(groupKmu, 1/((groupKsigma+0.0001)^2))
}

# MODEL-SPECIFIC: CALCULATION OF PRESENT SUBJECTIVE VALUES
for (t in 1:length(ID)) {
    VA[t] <- A[t] * (exp(-k[ID[t]]*DA[t]))
    VB[t] <- B[t] * (exp(-k[ID[t]]*DB[t]))
}


# RESPONSE ERROR PARAMETERS ====================================================
# comparison acuity (alpha) hyperprior
groupALPHAmu        ~ dnorm(0,1/(100^2)) T(0,)
groupALPHAsigma     ~ dunif(0,100) 
# error rates (epsilon) hyperprior
groupW          ~ dbeta(1.1, 10.9)  # mode for lapse rate
groupKminus2    ~ dgamma(0.5,0.5) # concentration parameter ## UPDATED SINCE PAPER
groupK          <- groupKminus2+2
for (p in 1:(nRealExperimentFiles+1)){ # +1 for unobserved participant
	alpha[p]    ~ dnorm(groupALPHAmu, 1/(groupALPHAsigma^2)) T(0,)
    epsilon[p]  ~ dbeta(groupW*(groupK-2)+1 , (1-groupW)*(groupK-2)+1 ) T(,0.5)
}


# MODEL IN-SPECIFIC CODE BELOW... SHOULD NOT CHANGE ACROSS MODELS ==============

# Psychometric function
for (t in 1:length(ID)) {
	P[t] <- epsilon[ID[t]] + (1-2*epsilon[ID[t]]) * phi( (VB[t]-VA[t]) / alpha[ID[t]] )
}

# response likelihood
for (t in 1:length(ID)) {
	R[t]  ~ dbern(P[t]) # likelihood of actual response
}

# POSTERIOR PREDICTION
for (t in 1:length(ID)) {
	Rpostpred[t] ~ dbern(P[t])
}

}
