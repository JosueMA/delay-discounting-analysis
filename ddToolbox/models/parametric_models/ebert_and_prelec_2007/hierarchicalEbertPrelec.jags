# RANDOM FACTORS:   k[p], tau[p], epsilon[p], alpha[p]
# HYPER-PRIORS ON:  k[p], tau[p], epsilon[p], alpha[p]

model{

# DISCOUNT FUNCTION PARAMETERS =================================================
# RANDOM (BY PARTICIPANT) FACTORS; HYPER-PRIORS = YES 

groupKmu       ~ dnorm(0,1/(0.5^2))
groupKsigma    ~ dexp(0.5)

groupTAUmu          ~ dnorm(1,1/(1^2))       
groupTAUsigma     ~ dexp(0.5) 

for (p in 1:(nRealExperimentFiles+1)){ # +1 for unobserved participant
    # using reparameterisation to avoid funnel of hell
    k_offset[p]     ~ dnorm(0,1)
    k[p]            <- groupKmu + k_offset[p] * groupKsigma
    
    # TODO: use reparameterisation to avoid funnel of hell
    #TAU_offset[p]  ~ dnorm(0,1)
    #tau[p]         <- groupTAUmu + TAU_offset[p] * groupTAUsigma
    
    tau[p]          ~ dnorm(groupTAUmu, groupTAUsigma) T(0,)
}

# MODEL-SPECIFIC: CALCULATION OF PRESENT SUBJECTIVE VALUES
for (t in 1:length(ID)) {
	# calculate present subjective value for each reward
    VA[t] <- A[t] * (exp( - (k[ID[t]] * DA[t])^tau[ID[t]] ) )
	VB[t] <- B[t] * (exp( - (k[ID[t]] * DB[t])^tau[ID[t]] ) )
}

# RESPONSE ERROR PARAMETERS ====================================================
# comparison acuity (alpha)
groupALPHAmu        ~ dnorm(0,1/(100^2)) T(0,) ## UPDATED SINCE PAPER
groupALPHAsigma     ~ dexp(0.5)

# error rates (epsilon)
groupW          ~ dbeta(1.1, 10.9)  # mode for lapse rate
groupKminus2    ~ dgamma(0.5,0.5) # concentration parameter ## UPDATED SINCE PAPER
groupK          <- groupKminus2+2

epsilon_alpha <- groupW*(groupK-2)+1
epsilon_beta <- (1-groupW)*(groupK-2)+1

for (p in 1:(nRealExperimentFiles+1)){ # +1 for unobserved participant
	epsilon[p]  ~ dbeta(epsilon_alpha , epsilon_beta ) T(,0.5)

    # using reparameterisation to avoid funnel of hell
    alpha_offset[p] ~ dnorm(0,1) T(0,)
    alpha[p] <- groupALPHAmu + alpha_offset[p] * groupALPHAsigma
}


# MODEL IN-SPECIFIC CODE BELOW... SHOULD NOT CHANGE ACROSS MODELS ==============

# Psychometric function
for (t in 1:length(ID)) {
	P[t] <- epsilon[ID[t]] + (1-2*epsilon[ID[t]]) * phi( (VB[t]-VA[t]) / alpha[ID[t]] )
}

# response likelihood
for (t in 1:length(ID)) {
	R[t]  ~ dbern(P[t]) # likelihood of actual response
}

# POSTERIOR PREDICTION
for (t in 1:length(ID)) {
	Rpostpred[t] ~ dbern(P[t])
}

}
