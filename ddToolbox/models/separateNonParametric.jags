# Discount Fraction estimated separately for each delay

# **** WARNING ****
# We also assume reward A is immediate, ie DA=0


# Observed data being provided:
# - participantIndexList
# - T (vector of trials per participant)
# - A, B
# - uniqueDelays
# - delayLookUp[t] is an index into the delay

# Parameters
# - alpha
# - epsilon
# - discountFraction: a vector of params, one for each unique delay

model{

# group-level priors
alpha   ~ dexp(0.01)
epsilon ~ dbeta(1.1 , 10.9)


# PRIOR OVER DISCOUNT FRACTION
for (p in participantIndexList){
	for (d in 1:length(uniqueDelays)) {
		discountFraction[p,d] ~ dnorm( 1, 1/ 1^2) T(0,)
	}
}

#  phi() cannot be vectorised

for (t in 1:length(ID)) {
	VA[t] <- A[t] # NO DELAY FOR A
	VB[t] <- (B[t]) * discountFraction[ID[t], delayLookUp[t] ]

	P[t] <- epsilon + (1-2*epsilon) * phi( (VB[t]-VA[t]) / alpha )

	R[t]         ~ dbern(P[t]) # likelihood of actual response
	Rpostpred[t] ~ dbern(P[t]) # posterior predicted response
}

}
