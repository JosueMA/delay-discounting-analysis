# JAGS model for delay discounting.
# - model of multiple participants
# - models the magnitude effect
# - separate parameters (for each participant) for:
#   - m
#   - c
#   - epsilon (lapse rate)
#   - alpha
# - no Heirarchical estimation at all

model{

	# WARNING  ************************************************************
	# These priors have been guesstimated based on what is reasonable. But 
	# if you are using this participant-level only analysis then you will 
	# want to think about what appropriate priors are.

    w <- 0.01   # mode for the beta distribution of lapse rates
    k <- 5      # concentration param for the beta distribution of lapse rates

    # Seperate and indendent parameter estimates for each person
    for (p in 1:nParticipants){
        m[p]    ~ dnorm(-0.243, 1/10)
        c[p]    ~ dnorm(0, 1/100)
        alpha[p]~ dexp(0.01)
        epsilon[p] ~ dbeta( w*(k-2)+1 , (1-w)*(k-2)+1 ) T(,0.5)
    }
    # These priors are seperated from the data and are only included here 
	# so that we can plot the prior distributions. They are duplicated from
	# the priors above.
    mprior    ~ dnorm(-0.243, 1/10)
    cprior    ~ dnorm(0, 1/100)
    alphaprior ~ dexp(0.01)
    epsilonprior   ~ dbeta( w*(k-2)+1 , (1-w)*(k-2)+1 )
	# *********************************************************************


    for (p in 1:nParticipants){

        # loop over the number of trials for this participant
        for (t in 1:T[p]) {

            # MAGNITUDE EFFECT
            # what is log(k) on this trial?
            lkA[p,t] <- m[p]*log(abs(A[p,t]))+c[p]
            lkB[p,t] <- m[p]*log(abs(B[p,t]))+c[p]

            # calculate present subjective value for each reward
            VA[p,t] <- A[p,t] / (1+(exp(lkA[p,t])*DA[p,t]))
            VB[p,t] <- B[p,t] / (1+(exp(lkB[p,t])*DB[p,t]))

            # psychometric function
            P[p,t] <- epsilon[p] + (1-2*epsilon[p]) * phi( (VB[p,t]-VA[p,t]) / alpha[p] )

            # response
            R[p,t] ~ dbin(P[p,t],1)

            # posterior predictive
            #Rpostpred[t] ~ dbin(P[t],1)
        }
    }
}
