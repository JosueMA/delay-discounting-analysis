# JAGS model for delay discounting.
# - model of multiple participants
# - DOES NOT model the magnitude effect
# - separate parameters (for each participant) for lapse rate and alpha
# - Hierarchical estimation of discount rates

model{

    # Group-level hyperpriors ------------------------
    groupLogKmu     ~ dnorm(-1,1/5)
    groupKsigma  ~ dunif(0,100)
    # for lapse rates ~~~~~~~~~~~
    groupW       ~ dbeta(1.1, 10.9) # mode for lapse rate
    groupKminus2 ~ dgamma(0.01,0.01)
    groupK <- groupKminus2+2



    # Participant-level ------------------------------
    # HIERACHICAL i.e. parameter estimates for each person, but each variable is drawn from a group-level distribution.
    for (p in 1:nParticipants){
        participantLogK[p] ~ dnorm(groupLogKmu, 1/groupKsigma)
        lr[p]   ~ dbeta( groupW*(groupK-2)+1 , (1-groupW)*(groupK-2)+1 )
    }
    # Participant-level, but NON-HEIRACHICAL. Seperate and indendent parameter estimates for each person
    for (p in 1:nParticipants){
        alpha[p]~ dexp(0.001)
    }

    # same priors for all participants
    lrprior   ~ dbeta( groupW*(groupK-2)+1 , (1-groupW)*(groupK-2)+1)
    alphaprior~ dexp(0.001)
    participantLogKprior ~ dnorm(groupLogKmu, 1/groupKsigma)

    # trial-level ------------------------------------
    for (t in 1:totalTrials) {

        # MAGNITUDE EFFECT
        # what is log(k) on this trial?
        lk[t] <- participantLogK[ID[t]]

        # present value of B
        PB[t] <- B[t] / (1+(exp(lk[t])*D[t]))

        # psychometric function
        P[t] <- lr[ID[t]] + (1-2*lr[ID[t]]) * phi( (PB[t]-A[t]) / alpha[ID[t]] )

        # response
        R[t] ~ dbin(P[t],1)

    }
}
